//@version=4
// Copyright (c) 2018-present, Alex Orekhov (everget)
// Schaff Trend Cycle script may be freely distributed under the MIT license.

study("Schaff Trend Cycle", shorttitle="STC")

// values for 15m candle

stc_fastLength = input(title='MACD Fast Length', defval=7)
stc_slowLength = input(title='MACD Slow Length', defval=49)
stc_cycleLength = input(title='Cycle Length', defval=20)
stc_d1Length = input(title='1st %D Length', defval=3)
stc_d2Length = input(title='2nd %D Length', defval=9)

stc_src = input(title="Source", type=input.source, defval=close)
stc_upper = input(title="Upper Band", type=input.integer, defval=80)
stc_lower = input(title="Lower Band", type=input.integer, defval=20)
stc_highlightBreakouts = input(title="Highlight Breakouts ?", type=input.bool, defval=true)

macd = ema(stc_src, stc_fastLength) - ema(stc_src, stc_slowLength)
k = nz(fixnan(stoch(macd, macd, macd, stc_cycleLength)))
d = ema(k, stc_d1Length)
kd = nz(fixnan(stoch(d, d, d, stc_cycleLength)))
stc = ema(kd, stc_d2Length)
stc := max(min(stc, 100), 0)

stcColor1 = stc > stc[1] ? color.green : color.red
stcColor2 = stc > stc_upper ? color.green : stc <= stc_lower ? color.red : color.orange

stcColor = stc_highlightBreakouts ? stcColor2 : stcColor1
stcPlot = plot(stc, title="STC", color=stcColor, transp=0)

stc_upperLevel = plot(stc_upper, title="Upper", color=color.gray)
hline(50, title="Middle", linestyle=hline.style_dotted)
stc_lowerLevel = plot(stc_lower, title="Lower", color=color.gray)

fill(stc_upperLevel, stc_lowerLevel, title="Middle Zone", color=#f9cb9c, transp=80)

stc_upperFillColor = stc > stc_upper and stc_highlightBreakouts ? color.green : na
stc_lowerFillColor = stc < stc_lower and stc_highlightBreakouts ? color.red : na

fill(stc_upperLevel, stcPlot, color=stc_upperFillColor, transp=80)
fill(stc_lowerLevel, stcPlot, color=stc_lowerFillColor, transp=80)

buySignal = crossover(stc, stc_lower)
sellSignal = crossunder(stc, stc_upper)
stc_upperCrossover = crossover(stc, stc_upper)
stc_upperCrossunder = crossunder(stc, stc_upper)
stc_lowerCrossover = crossover(stc, stc_lower)
stc_lowerCrossunder = crossunder(stc, stc_lower)

plotshape(stc_upperCrossunder ? stc_upper : na, title="Sell Signal", location=location.absolute, style=shape.circle, size=size.tiny, color=color.red, transp=0)
plotshape(stc_lowerCrossover ? stc_lower : na, title="Buy Signal", location=location.absolute, style=shape.circle, size=size.tiny, color=color.green, transp=0)

alertcondition(buySignal, title="Alert: Buy", message="STC: Buy!\nSymbol: {{exchange}}:{{ticker}}\nPrice: {{close}}")
alertcondition(sellSignal, title="Alert: Sell", message="STC: Sell!\nSymbol: {{exchange}}:{{ticker}}\nPrice: {{close}}")
alertcondition(stc_upperCrossover, title="Alert: Upper Band Crossover", message="STC: Upper Band Crossover!\nSymbol: {{exchange}}:{{ticker}}\nPrice: {{close}}")
alertcondition(stc_upperCrossunder, title="Alert: Upper Band Crossunder", message="STC: Upper Band Crossunder!\nSymbol: {{exchange}}:{{ticker}}\nPrice: {{close}}")
alertcondition(stc_lowerCrossover, title="Alert: Lower Band Crossover", message="STC: Lower Band Crossover!\nSymbol: {{exchange}}:{{ticker}}\nPrice: {{close}}")
alertcondition(stc_lowerCrossunder, title="Alert: Lower Band Crossunder", message="STC: Lower Band Crossunder!\nSymbol: {{exchange}}:{{ticker}}\nPrice: {{close}}")
